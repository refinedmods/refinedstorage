buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7"
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'net.minecraftforge.gradle.forge'

version = "1.0.1"
group = "refinedstorage"
archivesBaseName = "refinedstorage"

sourceCompatibility = 1.8
targetCompatibility = 1.8

minecraft {
    version = "1.10.2-12.18.1.2084"
    runDir = "run"
    useDepAts = true
    mappings = "snapshot_20160910"
}

repositories {
    maven {
        url "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        url "http://maven.epoxide.xyz"
    }
    maven {
        name = "ic2"
        url = "http://maven.ic2.player.to/"
    }
    maven {
        url "http://maven.amadornes.com/"
    }
}

dependencies {
    deobfCompile "mezz.jei:jei_1.10.2:3.9.+"
    compile "net.darkhax.tesla:Tesla:1.10-1.2.+"
    compile "net.industrial-craft:industrialcraft-2:2.6.66-ex110:api"
    deobfCompile "MCMultiPart:MCMultiPart:1.2.1+:universal"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier 'deobf'
}

task apiJar(type: Jar, dependsOn: 'sourceMainJava') {
    from sourceSets.main.allSource
	from sourceSets.main.output
	include 'refinedstorage/api/**/*'
	classifier 'api'
}

artifacts {
	archives deobfJar, apiJar
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            groupId = project.group
            artifactId = project.archivesBaseName
            version = project.version + '-' + System.getenv('TRAVIS_BUILD_NUMBER')
            artifact source: jar, classifier: 'obf', extension: 'jar'
            artifact deobfJar
            artifact apiJar
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['MyPublication']
    publish = true
    pkg {
        repo = 'dev'
        name = 'refinedstorage'
        version {
            name = project.version + '-' + System.getenv('TRAVIS_BUILD_NUMBER')
            released = new Date()
        }
    }
}