buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
    }
}

plugins {
    id 'com.matthewprenger.cursegradle' version '1.4.0'
}

repositories {
    maven {
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        url = "https://maven.theillusivec4.top/"
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

group = 'com.refinedmods'
archivesBaseName = 'refinedstorage'
version = '1.10.0-beta.5'

if (System.getenv('GITHUB_SHA') != null) {
    version += '+' + System.getenv('GITHUB_SHA').substring(0, 7)
}

if (System.getenv('RELEASE') != null) {
    version = System.getenv('RELEASE').substring(1) // remove 'v'
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xmaxerrs" << "999"
}

minecraft {
    mappings channel: 'official', version: '1.18.1'
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', ''
            property 'forge.logging.console.level', 'debug'
            mods {
                refinedstorage {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            mods {
                refinedstorage {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'refinedstorage', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources')
            mods {
                refinedstorage {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets {
    main.resources.srcDirs += 'src/generated/resources'
}

processResources {
    inputs.property 'version', project.version
    filesMatching('META-INF/mods.toml') {
        expand 'version': project.version
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.18.1-39.0.59'

    compileOnly fg.deobf("mezz.jei:jei-1.18.1:9.1.0.41:api")
    runtimeOnly fg.deobf("mezz.jei:jei-1.18.1:9.1.0.41")

    compileOnly fg.deobf('curse.maven:mouse-tweaks-60089:3546383')

    compileOnly fg.deobf('curse.maven:crafting-tweaks-233071:3545611')

    runtimeOnly fg.deobf("top.theillusivec4.curios:curios-forge:1.18-5.0.2.4")
    compileOnly fg.deobf("top.theillusivec4.curios:curios-forge:1.18-5.0.2.4:api")
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "refinedstorage",
                "Specification-Vendor"    : "refinedmods",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "refinedmods",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

if (System.getenv("CURSEFORGE_TOKEN") != null) {
    curseforge {
        apiKey = System.getenv("CURSEFORGE_TOKEN")
        project {
            id = '243076'
            changelog = System.getenv("CHANGELOG")
            changelogType = 'markdown'
            releaseType = project.version.toString().contains('beta') ? 'beta' : (project.version.toString().contains('alpha') ? 'alpha' : 'release')
            addGameVersion "1.18.1"
            mainArtifact(jar) {
                displayName = "v$project.version"
            }
        }
    }
}

jar.finalizedBy('reobfJar')

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/refinedmods/refinedstorage")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}
