buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7"
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'net.minecraftforge.gradle.forge'

version = "1.5.14"
group = "refinedstorage"
archivesBaseName = "refinedstorage"

sourceCompatibility = 1.8
targetCompatibility = 1.8

minecraft {
    version = "1.12-14.21.1.2400"
    runDir = "run"
    useDepAts = true
    mappings = "snapshot_20170707"
}

repositories {
    maven {
        url "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        url "http://maven.amadornes.com"
    }
    maven {
        url "http://maven.cil.li"
    }
    maven {
        url "https://dl.bintray.com/jaquadro/dev"
    }
}

dependencies {
    deobfCompile "mezz.jei:jei_1.12:4.7.1.71:api"
    runtime "mezz.jei:jei_1.12:4.7.1.71"
    deobfCompile "MCMultiPart2:MCMultiPart:2.2.2"
    // deobfCompile "li.cil.oc:OpenComputers:MC1.11.2-1.7.0.28:api"
    deobfCompile "com.jaquadro.minecraft.storagedrawers:StorageDrawers:1.12-5.2.2:api"
    // runtime "com.jaquadro.minecraft.storagedrawers:StorageDrawers:1.12-5.2.2"
    // runtime "com.jaquadro.minecraft.chameleon:Chameleon:1.12-4.1.0"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    include "com/raoulvdberge/refinedstorage/**"
    include "assets/**"
    include "mcmod.info"
    include "pack.mcmeta"
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier 'deobf'
}

task apiJar(type: Jar, dependsOn: 'sourceMainJava') {
    from sourceSets.main.allSource
    from sourceSets.main.output
    include 'com/raoulvdberge/refinedstorage/api/**/*'
    classifier 'api'
}

artifacts {
    archives deobfJar
    archives apiJar
    archives sourceJar
    archives jar
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            groupId = project.group
            artifactId = project.archivesBaseName
            version = project.version + '-' + System.getenv('TRAVIS_BUILD_NUMBER')
            artifact deobfJar
            artifact sourceJar
            artifact apiJar
            artifact jar
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['MyPublication']
    publish = true
    pkg {
        repo = 'dev'
        name = 'refinedstorage'
        version {
            name = project.version + '-' + System.getenv('TRAVIS_BUILD_NUMBER')
            released = new Date()
        }
    }
}